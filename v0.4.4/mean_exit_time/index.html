<!DOCTYPE html>
<html lang="en"><head><meta charset="UTF-8"/><meta name="viewport" content="width=device-width, initial-scale=1.0"/><title>Example IX: Mean exit time problems · FiniteVolumeMethod.jl</title><script data-outdated-warner src="../assets/warner.js"></script><link rel="canonical" href="https://DanielVandH.github.io/FiniteVolumeMethod.jl/mean_exit_time/"/><link href="https://cdnjs.cloudflare.com/ajax/libs/lato-font/3.0.0/css/lato-font.min.css" rel="stylesheet" type="text/css"/><link href="https://cdnjs.cloudflare.com/ajax/libs/juliamono/0.045/juliamono.min.css" rel="stylesheet" type="text/css"/><link href="https://cdnjs.cloudflare.com/ajax/libs/font-awesome/5.15.4/css/fontawesome.min.css" rel="stylesheet" type="text/css"/><link href="https://cdnjs.cloudflare.com/ajax/libs/font-awesome/5.15.4/css/solid.min.css" rel="stylesheet" type="text/css"/><link href="https://cdnjs.cloudflare.com/ajax/libs/font-awesome/5.15.4/css/brands.min.css" rel="stylesheet" type="text/css"/><link href="https://cdnjs.cloudflare.com/ajax/libs/KaTeX/0.13.24/katex.min.css" rel="stylesheet" type="text/css"/><script>documenterBaseURL=".."</script><script src="https://cdnjs.cloudflare.com/ajax/libs/require.js/2.3.6/require.min.js" data-main="../assets/documenter.js"></script><script src="../siteinfo.js"></script><script src="../../versions.js"></script><link class="docs-theme-link" rel="stylesheet" type="text/css" href="../assets/themes/documenter-dark.css" data-theme-name="documenter-dark" data-theme-primary-dark/><link class="docs-theme-link" rel="stylesheet" type="text/css" href="../assets/themes/documenter-light.css" data-theme-name="documenter-light" data-theme-primary/><script src="../assets/themeswap.js"></script></head><body><div id="documenter"><nav class="docs-sidebar"><div class="docs-package-name"><span class="docs-autofit"><a href="../">FiniteVolumeMethod.jl</a></span></div><form class="docs-search" action="../search/"><input class="docs-search-query" id="documenter-search-query" name="q" type="text" placeholder="Search docs"/></form><ul class="docs-menu"><li><a class="tocitem" href="../">Home</a></li><li><a class="tocitem" href="../interface/">Interface</a></li><li><a class="tocitem" href="../docstrings/">Docstrings</a></li><li><a class="tocitem" href="../example_list/">List of Examples and Setup</a></li><li><a class="tocitem" href="../diffusion_equation/">Example I: Diffusion equation on a square plate</a></li><li><a class="tocitem" href="../diffusion_equation_on_a_wedge/">Example II: Diffusion equation in a wedge with mixed boundary conditions</a></li><li><a class="tocitem" href="../reaction_diffusion/">Example III: Reaction-diffusion equation with a time-dependent Dirichlet boundary condition on a disk</a></li><li><a class="tocitem" href="../porous_medium/">Example IV: Porous-medium equation</a></li><li><a class="tocitem" href="../porous_fisher/">Example V: Porous-Fisher equation and travelling waves</a></li><li><a class="tocitem" href="../interpolants/">Example VI: Using the linear interpolants</a></li><li><a class="tocitem" href="../annulus/">Example VII: Diffusion equation on an annulus</a></li><li><a class="tocitem" href="../laplace/">Example VIII: Laplace&#39;s equation</a></li><li class="is-active"><a class="tocitem" href>Example IX: Mean exit time problems</a><ul class="internal"><li><a class="tocitem" href="#Circle"><span>Circle</span></a></li><li><a class="tocitem" href="#Ellipse"><span>Ellipse</span></a></li><li><a class="tocitem" href="#Annulus"><span>Annulus</span></a></li></ul></li><li><a class="tocitem" href="../math/">Mathematical Details</a></li></ul><div class="docs-version-selector field has-addons"><div class="control"><span class="docs-label button is-static is-size-7">Version</span></div><div class="docs-selector control is-expanded"><div class="select is-fullwidth is-size-7"><select id="documenter-version-selector"></select></div></div></div></nav><div class="docs-main"><header class="docs-navbar"><nav class="breadcrumb"><ul class="is-hidden-mobile"><li class="is-active"><a href>Example IX: Mean exit time problems</a></li></ul><ul class="is-hidden-tablet"><li class="is-active"><a href>Example IX: Mean exit time problems</a></li></ul></nav><div class="docs-right"><a class="docs-edit-link" href="https://github.com/DanielVandH/FiniteVolumeMethod.jl/blob/main/docs/src/mean_exit_time.md#" title="Edit on GitHub"><span class="docs-icon fab"></span><span class="docs-label is-hidden-touch">Edit on GitHub</span></a><a class="docs-settings-button fas fa-cog" id="documenter-settings-button" href="#" title="Settings"></a><a class="docs-sidebar-button fa fa-bars is-hidden-desktop" id="documenter-sidebar-button" href="#"></a></div></header><article class="content" id="documenter-page"><h1 id="Example-IX:-Mean-exit-time-problems"><a class="docs-heading-anchor" href="#Example-IX:-Mean-exit-time-problems">Example IX: Mean exit time problems</a><a id="Example-IX:-Mean-exit-time-problems-1"></a><a class="docs-heading-anchor-permalink" href="#Example-IX:-Mean-exit-time-problems" title="Permalink"></a></h1><p>In this example, we will investigate some simple mean exit time problems. In particular, the aim is to reproduce some of the figures from the numerical solutions in my other work on mean exit time at <a href="https://iopscience.iop.org/article/10.1088/1367-2630/abe60d">https://iopscience.iop.org/article/10.1088/1367-2630/abe60d</a> and <a href="https://iopscience.iop.org/article/10.1088/1751-8121/ac4a1d">https://iopscience.iop.org/article/10.1088/1751-8121/ac4a1d</a>.</p><p>Fistly, mean exit time problems with linear diffusion can be defined according to</p><p class="math-container">\[\begin{equation*}
\begin{array}{rcll}
D\boldsymbol{\nabla}^2 T(\boldsymbol x) &amp; = &amp; -1 &amp; \boldsymbol x \in \Omega, \\
T(\boldsymbol x) &amp; = &amp; 0 &amp; \boldsymbol x \in \partial\Omega,
\end{array}
\end{equation*}\]</p><p>for some diffusivity <span>$D$</span>. <span>$T(\boldsymbol x)$</span> is the mean exit time at <span>$\boldsymbol x$</span>, meaning the average amount of time it would take a particle starting at <span>$\boldsymbol x$</span> to exit the domain through <span>$\partial\Omega$</span>. For this interpretation of <span>$T$</span>, we are letting <span>$D = \mathcal P\delta^2/(4\tau)$</span>, where <span>$\delta&gt;0$</span> is the step length of the particle, <span>$\tau &gt; 0$</span> is the duration between steps, and <span>$\mathcal P \in [0, 1]$</span> is the probability that the particle actually moves at a given time step.</p><p>To define the problem with our interface, remember that we use the PDE (using the reaction-diffusion formulation rather than the formulation with <span>$\boldsymbol q$</span>)</p><p class="math-container">\[\begin{equation*}
\dfrac{\partial T}{\partial t} = \boldsymbol{\nabla} \boldsymbol{\cdot} \left[ D(x, y, t, T)\boldsymbol{\nabla} T(x, y, t)\right] + R(x, y, t, T).
\end{equation*}\]</p><p>Since this is a steady problem, <span>$\partial T/\partial t = 0$</span>, meaning</p><p class="math-container">\[\begin{equation*}
\boldsymbol{\nabla} \boldsymbol{\cdot} \left[ D(x, y, t, T)\boldsymbol{\nabla} T(x, y, t)\right] = -R(x, y, t, T).
\end{equation*}\]</p><p>Therefore, we should define a problem with <span>$D(x, y, t, T) = D$</span> and <span>$R(x, y, t, T) = 1$</span>. Let&#39;s now give the examples.</p><h2 id="Circle"><a class="docs-heading-anchor" href="#Circle">Circle</a><a id="Circle-1"></a><a class="docs-heading-anchor-permalink" href="#Circle" title="Permalink"></a></h2><p>Let&#39;s first solve the problem on a unit circle. In this case, the exact solution is <span>$T(x, y) = (1 - r^2)/(4D)$</span>, <span>$r = \sqrt{x^2 + y^2}$</span>. For the initial guess for <span>$T$</span>, we will just set <span>$T=1$</span> everywhere and <span>$T=0$</span> on the boundary.</p><pre><code class="language-julia hljs">using FiniteVolumeMethod
using DelaunayTriangulation
using CairoMakie
using OrdinaryDiffEq
using SteadyStateDiffEq
using Test
using LinearAlgebra
using StatsBase

R = 1.0
θ = collect(LinRange(0, 2π, 100))
θ[end] = θ[begin]
x = R .* cos.(θ)
y = R .* sin.(θ)
boundary_nodes, points = convert_boundary_points_to_indices(x, y)
tri = triangulate(points; boundary_nodes)
A = get_total_area(tri)
refine!(tri; max_area=1e-3A, min_angle=33.0)
mesh = FVMGeometry(tri)
bc = (x, y, t, u, p) -&gt; zero(u)
type = :D
BCs = BoundaryConditions(mesh, bc, type)
D = 2.5e-5
initial_guess = ones(num_points(tri))
initial_guess[get_boundary_nodes(tri)] .= 0.0
diffusion_function = (x, y, t, u, D) -&gt; D
diffusion_parameters = D
reaction = (x, y, t, u, p) -&gt; one(u)
final_time = Inf # doesn&#39;t matter in this case, but we still need to provide it
prob = FVMProblem(mesh, BCs;
    diffusion_function,
    diffusion_parameters,
    reaction_function=reaction,
    final_time=final_time,
    initial_condition=initial_guess,
    steady=true)

# Solve 
alg = DynamicSS(Rosenbrock23())
sol = solve(prob, alg)

# Visualise 
fig = Figure(fontsize=38)
ax = Axis(fig[1, 1], xlabel=L&quot;x&quot;, ylabel=L&quot;y&quot;)
pt_mat = get_points(tri)
T_mat = [T[j] for T in each_triangle(tri), j in 1:3]
msh = mesh!(ax, pt_mat, T_mat, color=sol, colorrange=(0, 10000))
Colorbar(fig[1, 2], msh)

# Test 
exact = [(R^2 - norm(p)^2)/(4D) for p in each_point(tri)]
error_fnc = (fvm_sol, exact_sol) -&gt; 100abs(fvm_sol - exact_sol)/maximum(exact)
@test median(error_fnc.(exact, sol)) &lt; 0.2</code></pre><figure>
    <img src='../figures/circle_mean_exit_time.png', alt='Mean exit time on a disk'><br>
</figure><p>We can now extend this problem by considering a perturbed domain. In particular, we consider a circle with boundary defined by <span>$\mathcal R(\theta) = 1 + \varepsilon g(\theta)$</span> for a small perturbation <span>$\varepsilon = 1/20$</span> and <span>$g(\theta) =\sin(3\theta) + \cos(5\theta) - \sin(\theta)$</span>. For the initial guess, we will use the solution from the unperturbed problem.</p><pre><code class="language-julia hljs">using FiniteVolumeMethod
using DelaunayTriangulation
using CairoMakie
using OrdinaryDiffEq
using SteadyStateDiffEq
using LinearSolve
using LinearAlgebra
using FastLapackInterface

R = 1.0
θ = LinRange(0, 2π, 100)
ε = 1 / 20
g = θ -&gt; sin(3θ) + cos(5θ) - sin(θ)
R_bnd = 1 .+ ε .* g.(θ)
x = R_bnd .* cos.(θ)
y = R_bnd .* sin.(θ)
x[end] = x[begin]
y[end] = y[begin] # ensure circular array 
boundary_nodes, points = convert_boundary_points_to_indices(x, y)
tri = triangulate(points; boundary_nodes)
A = get_total_area(tri)
refine!(tri; max_area=1e-3A, min_angle=33.0)
mesh = FVMGeometry(tri)
bc = (x, y, t, u, p) -&gt; zero(u)
type = :D
BCs = BoundaryConditions(mesh, bc, type)
D = 2.5e-5
initial_guess = [(R^2 - norm(p)^2)/(4D) for p in each_point(tri)]
initial_guess[get_boundary_nodes(tri)] .= 0.0
diffusion_function = (x, y, t, u, D) -&gt; D
diffusion_parameters = D
reaction = (x, y, t, u, p) -&gt; one(u)
final_time = Inf # doesn&#39;t matter in this case, but we still need to provide it
prob = FVMProblem(mesh, BCs;
    diffusion_function,
    diffusion_parameters,
    reaction_function=reaction,
    final_time=final_time,
    initial_condition=initial_guess,
    steady=true)
alg = DynamicSS(TRBDF2(linsolve=FastLUFactorization()))
sol = solve(prob, alg, parallel = true)
fig = Figure(fontsize=38)
ax = Axis(fig[1, 1], xlabel=L&quot;x&quot;, ylabel=L&quot;y&quot;)
pt_mat = get_points(tri)
T_mat = [T[j] for T in each_triangle(tri), j in 1:3]
msh = mesh!(ax, pt_mat, T_mat, color=sol, colorrange=(0, 10000))
Colorbar(fig[1, 2], msh)</code></pre><figure>
    <img src='../figures/perturbed_circle_mean_exit_time.png', alt='Mean exit time on a perturbed circle'><br>
</figure><h2 id="Ellipse"><a class="docs-heading-anchor" href="#Ellipse">Ellipse</a><a id="Ellipse-1"></a><a class="docs-heading-anchor-permalink" href="#Ellipse" title="Permalink"></a></h2><p>Now let&#39;s consider the same problem but on an ellipse. Here, for an unperturbed ellipse with width <span>$2a&gt;0$</span> and height <span>$2b&gt;0$</span> (<span>$a&gt;b$</span>), the solution is</p><p class="math-container">\[T(x, y) = \frac{a^2b^2}{2D(a^2+b^2)}\left[1-\frac{x^2}{y^2}-\frac{y^2}{b^2}\right].\]</p><p>The code below solves the unperturbed problem.</p><pre><code class="language-julia hljs">using FiniteVolumeMethod
using DelaunayTriangulation
using CairoMakie
using OrdinaryDiffEq
using SteadyStateDiffEq
using Test
using LinearAlgebra
using StatsBase
using LinearSolve
using Krylov

a = 2.0
b = 1.0
θ = LinRange(0, 2π, 100)
x = a * cos.(θ)
y = b * sin.(θ)
x[end] = x[begin]
y[end] = y[begin]
boundary_nodes, points = convert_boundary_points_to_indices(x, y)
tri = triangulate(points; boundary_nodes)
A = get_total_area(tri)
refine!(tri; max_area=1e-3A/2, min_angle = 33.0)
mesh = FVMGeometry(tri)
bc = (x, y, t, u, p) -&gt; zero(u)
type = :D
BCs = BoundaryConditions(mesh, bc, type)
D = 2.5e-5
initial_guess = ones(num_points(tri))
initial_guess[get_boundary_nodes(tri)] .= 0.0
diffusion_function = (x, y, t, u, D) -&gt; D
diffusion_parameters = D
reaction = (x, y, t, u, p) -&gt; one(u)
final_time = Inf
prob = FVMProblem(mesh, BCs;
    diffusion_function,
    diffusion_parameters,
    reaction_function=reaction,
    final_time=final_time,
    initial_condition=initial_guess,
    steady=true)
sol = solve(prob, DynamicSS(TRBDF2(linsolve=KrylovJL_GMRES())))
fig = Figure(fontsize=38)
ax = Axis(fig[1, 1], xlabel=L&quot;x&quot;, ylabel=L&quot;y&quot;, width=600, height=300)
pt_mat = get_points(tri)
T_mat = [T[j] for T in each_triangle(tri), j in 1:3]
msh = mesh!(ax, pt_mat, T_mat, color=sol, colorrange=(0, 16000))
Colorbar(fig[1, 2], msh)
resize_to_layout!(fig)
exact = [a^2 * b^2 / (2 * D * (a^2 + b^2)) * (1 - x^2 / a^2 - y^2 / b^2) for (x, y) in each_point(tri)]
error_fnc = (fvm_sol, exact_sol) -&gt; 100abs(fvm_sol - exact_sol) / maximum(exact)
@test median(error_fnc.(exact, sol)) &lt; 0.3</code></pre><figure>
    <img src='../figures/ellipse_mean_exit_time.png', alt='Mean exit time on an ellipse'><br>
</figure><p>Now we consider a perturbed ellipse. In particular, we take <span>$x = a(1 + \varepsilon g(\theta))\cos(\theta)$</span> and <span>$y = b(1 + \varepsilon h(\theta))\sin(\theta)$</span>, with <span>$\varepsilon 1/20$</span>, <span>$g(\theta) = \sin(3\theta) + \cos(5\theta) - \sin(\theta)$</span>, and <span>$h(\theta) = \cos(3\theta) + \sin(5\theta) - \cos(\theta)$</span>. We will start our initial guess at the zero vector.</p><pre><code class="language-julia hljs">using FiniteVolumeMethod
using DelaunayTriangulation
using CairoMakie
using OrdinaryDiffEq
using SteadyStateDiffEq
using Test
using LinearSolve
using Krylov

a = 2.0
b = 1.0
θ = LinRange(0, 2π, 100)
ε = 1 / 20
g = θ -&gt; sin(3θ) + cos(5θ) - sin(θ)
h = θ -&gt; cos(3θ) + sin(5θ) - cos(θ)
x = a * (1 .+ ε .* g.(θ)) .* cos.(θ)
y = b * (1 .+ ε .* h.(θ)) .* sin.(θ)
x[end] = x[begin]
y[end] = y[begin]
boundary_nodes, points = convert_boundary_points_to_indices(x, y)
tri = triangulate(points; boundary_nodes)
A = get_total_area(tri)
refine!(tri; max_area=1e-4A/2, min_angle = 33.0)
mesh = FVMGeometry(tri)
bc = (x, y, t, u, p) -&gt; zero(u)
type = :D
BCs = BoundaryConditions(mesh, bc, type)
D = 2.5e-5
initial_guess = zeros(num_points(tri))
diffusion_function = (x, y, t, u, D) -&gt; D
diffusion_parameters = D
reaction = (x, y, t, u, p) -&gt; one(u)
final_time = Inf
prob = FVMProblem(mesh, BCs;
    diffusion_function,
    diffusion_parameters,
    reaction_function=reaction,
    final_time=final_time,
    initial_condition=initial_guess,
    steady=true)
sol = solve(prob, DynamicSS(TRBDF2(linsolve=KrylovJL_GMRES())))
fig = Figure(fontsize=38)
ax = Axis(fig[1, 1], xlabel=L&quot;x&quot;, ylabel=L&quot;y&quot;, width=600, height=300)
pt_mat = get_points(tri)
T_mat = [T[j] for T in each_triangle(tri), j in 1:3]
msh = mesh!(ax, pt_mat, T_mat, color=sol, colorrange=(0, 16000))
Colorbar(fig[1, 2], msh)
resize_to_layout!(fig)</code></pre><figure>
    <img src='../figures/perturbed_ellipse_mean_exit_time.png', alt='Mean exit time on a perturbed ellipse'><br>
</figure><h2 id="Annulus"><a class="docs-heading-anchor" href="#Annulus">Annulus</a><a id="Annulus-1"></a><a class="docs-heading-anchor-permalink" href="#Annulus" title="Permalink"></a></h2><p>Now we consider mean exit time on an annulus. The paper <a href="https://iopscience.iop.org/article/10.1088/1751-8121/ac4a1d">https://iopscience.iop.org/article/10.1088/1751-8121/ac4a1d</a> also considers heterogeneous annuli, but we will not do that here. We will consider a reflecting inner boundary condition with an absorbing outer boundary condition, so that <span>$\boldsymbol{\nabla}^2 T = -1/D$</span> in the annulus, <span>$\boldsymbol{\nabla} T \boldsymbol{\cdot} \hat{\boldsymbol n} = 0$</span> on the inner ring, and <span>$T = 0$</span> on the outer ring. The annulus we consider is <span>$R_1 &lt; r &lt; R_2$</span> with <span>$R_1 = 2$</span> and <span>$R_2 = 3$</span>, which corresponds to an exact solution <span>$T(x, y) = (R_2^2 - r^2)/(4D) + R_1^2\log(r/R_2)/(2D)$</span>, where <span>$r = \sqrt{x^2 + y^2}$</span>.</p><pre><code class="language-julia hljs">using FiniteVolumeMethod
using DelaunayTriangulation
using CairoMakie
using OrdinaryDiffEq
using SteadyStateDiffEq
using Test
using LinearAlgebra
using StatsBase
using LinearSolve
using Krylov

R₁ = 2.0
R₂ = 3.0
θ = (collect ∘ LinRange)(0, 2π, 250)
θ[end] = θ[begin]
x = [
    [R₂ .* cos.(θ)],
    [reverse(R₁ .* cos.(θ))] # inner boundaries are clockwise
]
y = [
    [R₂ .* sin.(θ)],
    [reverse(R₁ .* sin.(θ))] # inner boundaries are clockwise
]
boundary_nodes, points = convert_boundary_points_to_indices(x, y)
tri = triangulate(points; boundary_nodes)
A = get_total_area(tri)
refine!(tri; max_area = 1e-4A)
mesh = FVMGeometry(tri)
outer_bc = (x, y, t, u, p) -&gt; zero(u)
inner_bc = (x, y, t, u, p) -&gt; zero(u)
type = [:D, :N]
BCs = BoundaryConditions(mesh, [outer_bc, inner_bc], type)
D = 6.25e-4
initial_guess = zeros(num_points(tri))
diffusion_function = (x, y, t, u, D) -&gt; D
diffusion_parameters = D
reaction = (x, y, t, u, p) -&gt; one(u)
final_time = Inf
prob = FVMProblem(mesh, BCs;
    diffusion_function,
    diffusion_parameters,
    reaction_function=reaction,
    final_time=final_time,
    initial_condition=initial_guess,
    steady=true)
sol = solve(prob, DynamicSS(TRBDF2(linsolve=KrylovJL_GMRES())))
fig = Figure(fontsize=38)
ax = Axis(fig[1, 1], xlabel=L&quot;x&quot;, ylabel=L&quot;y&quot;, width=600, height=300)
pt_mat = get_points(tri)
T_mat = [T[j] for T in each_triangle(tri), j in 1:3]
msh = mesh!(ax, pt_mat, T_mat, color=sol, colorrange=(0, 900))
Colorbar(fig[1, 2], msh)
resize_to_layout!(fig)
exact = [(R₂^2 - norm(p)^2) / (4D) + R₁^2 * log(norm(p) / R₂) / (2D) for p in each_point(tri)]
error_fnc = (fvm_sol, exact_sol) -&gt; 100abs(fvm_sol - exact_sol) / maximum(exact)
@test median(error_fnc.(exact, sol)) &lt; 0.1</code></pre><figure>
    <img src='../figures/annulus_mean_exit_time.png', alt='Mean exit time on an annulus'><br>
</figure><p>Now let&#39;s take a perturbed annulus. In particular, we let the inner boundary be <span>$\mathcal R_1(\theta) = R_1(1 + \varepsilon g_1(\theta))$</span> with <span>$g_1(\theta) = \sin(3\theta) + \cos(5\theta)$</span> and <span>$\varepsilon = 1/20$</span>, and the outer boundary is <span>$\mathcal R_2(\theta) = R_2(1 + \varepsilon g_2(\theta))$</span> with <span>$g_2(\theta) = \cos(3\theta)$</span>.</p><pre><code class="language-julia hljs">using FiniteVolumeMethod
using DelaunayTriangulation
using CairoMakie
using OrdinaryDiffEq
using SteadyStateDiffEq
using Test
using LinearSolve
using Krylov

R₁ = 2.0
R₂ = 3.0
g₁ = θ -&gt; sin(3θ) + cos(5θ)
g₂ = θ -&gt; cos(3θ)
θ = (collect ∘ LinRange)(0, 2π, 250)
θ[end] = θ[begin] # close the boundary properly
ε = 1/20
inner_R = R₁ .* (1 .+ ε .* g₁.(θ))
outer_R = R₂ .* ( 1 .+ ε .* g₂.(θ))
x = [
    [outer_R .* cos.(θ)],
    [reverse(inner_R .* cos.(θ))] # inner boundaries are clockwise
]
y = [
    [outer_R .* sin.(θ)],
    [reverse(inner_R .* sin.(θ))] # inner boundaries are clockwise
]
boundary_nodes, points = convert_boundary_points_to_indices(x, y)
tri = triangulate(points; boundary_nodes)
A = get_total_area(tri)
refine!(tri; max_area=1e-4A)
mesh = FVMGeometry(tri)
outer_bc = (x, y, t, u, p) -&gt; zero(u)
inner_bc = (x, y, t, u, p) -&gt; zero(u)
type = [:D, :N]
BCs = BoundaryConditions(mesh, [outer_bc, inner_bc], type)
D = 6.25e-4
initial_guess = zeros(num_points(tri))
diffusion_function = (x, y, t, u, D) -&gt; D
diffusion_parameters = D
reaction = (x, y, t, u, p) -&gt; one(u)
final_time = Inf
prob = FVMProblem(mesh, BCs;
    diffusion_function,
    diffusion_parameters,
    reaction_function=reaction,
    final_time=final_time,
    initial_condition=initial_guess,
    steady=true)
sol = solve(prob, DynamicSS(TRBDF2(linsolve=KrylovJL_GMRES())), parallel = true)
fig = Figure(fontsize=38)
ax = Axis(fig[1, 1], xlabel=L&quot;x&quot;, ylabel=L&quot;y&quot;, width=600, height=300)
pt_mat = get_points(tri)
T_mat = [T[j] for T in each_triangle(tri), j in 1:3]
msh = mesh!(ax, pt_mat, T_mat, color=sol, colorrange=(0, 900))
Colorbar(fig[1, 2], msh)
resize_to_layout!(fig)</code></pre><figure>
    <img src='../figures/perturbed_annulus_mean_exit_time.png', alt='Mean exit time on a perturbed annulus'><br>
</figure></article><nav class="docs-footer"><a class="docs-footer-prevpage" href="../laplace/">« Example VIII: Laplace&#39;s equation</a><a class="docs-footer-nextpage" href="../math/">Mathematical Details »</a><div class="flexbox-break"></div><p class="footer-message">Powered by <a href="https://github.com/JuliaDocs/Documenter.jl">Documenter.jl</a> and the <a href="https://julialang.org/">Julia Programming Language</a>.</p></nav></div><div class="modal" id="documenter-settings"><div class="modal-background"></div><div class="modal-card"><header class="modal-card-head"><p class="modal-card-title">Settings</p><button class="delete"></button></header><section class="modal-card-body"><p><label class="label">Theme</label><div class="select"><select id="documenter-themepicker"><option value="documenter-light">documenter-light</option><option value="documenter-dark">documenter-dark</option></select></div></p><hr/><p>This document was generated with <a href="https://github.com/JuliaDocs/Documenter.jl">Documenter.jl</a> version 0.27.24 on <span class="colophon-date" title="Monday 24 April 2023 23:13">Monday 24 April 2023</span>. Using Julia version 1.8.5.</p></section><footer class="modal-card-foot"></footer></div></div></div></body></html>
